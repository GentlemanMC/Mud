buildscript {
    repositories {
        maven {
            url "https://maven.minecraftforge.net"
        }
        maven {
            url 'https://repo.spongepowered.org/maven'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version = "1.0"
group = "dev.zprestige"

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = project.forgeVersion
    runDir = "run"
    mappings = project.mcpVersion
    coreMod = "dev.zprestige.mud.mixins.tweaker.FMLTweaker"
    makeObfSourceJar = false
}

configurations {
    prestige
    compile.extendsFrom(prestige)
}

repositories {
    maven {
        url 'https://repo.spongepowered.org/maven/'
    }
    maven {
        url 'https://litarvan.github.io/maven'
    }
    mavenCentral()
}

dependencies {
    prestige group: 'club.minnced', name: 'java-discord-rpc', version: '2.0.1'
    prestige 'fr.litarvan:openauth:1.1.3'
    prestige("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'log4j-core'
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

shadowJar {
    dependencies {
        include(dependency('org.spongepowered:mixin'))
        include(dependency('club.minnced:java-discord-rpc:2.0.1'))
        include(dependency('club.minnced:discord-rpc-release:v3.3.0'))
        include(dependency('fr.litarvan:openauth:1.1.3'))
    }
    configurations = [project.configurations.prestige]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    classifier = "release"
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.mud.refmap.json"
}


reobf {
    shadowJar {
        mappingType = "SEARGE"
        classpath = sourceSets.main.compileClasspath
    }
}

jar {
    manifest {
        attributes(
                "FMLCorePlugin": "dev.zprestige.mud.mixins.tweaker.FMLTweaker",
                "ForceLoadAsMod": "true",
                "TweakOrder": 0,
                "FMLCorePluginContainsFMLMod": "true",
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "MixinConfigs": "mixins.mud.json",
                'FMLAT': 'mud_at.cfg'
        )
    }
}

build.dependsOn(shadowJar)
